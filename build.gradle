/*
 * Copyright 2018 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'nebula.release' version '15.2.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'io.codearte.nexus-staging' version '0.22.0'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group "net.wooga"

List<String> cliTasks = project.rootProject.gradle.startParameter.taskNames
if (cliTasks.contains("rc")) {
    cliTasks.remove("rc")
    cliTasks.add("candidate")
    project.rootProject.gradle.startParameter.setTaskNames(cliTasks)
}

configurations {
    rustLib {
        transitive = false
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    rustLib project('rust')

    testCompile "org.codehaus.groovy:groovy-all:2.4.15"
    testCompile "org.spockframework:spock-core:1.2-groovy-2.4"
    testCompile "com.wooga.spock.extensions:spock-unity-version-manager-extension:[0.1.0,1)"
}

task collectLibs() {
    dependsOn configurations.rustLib

    doLast {
        configurations.rustLib.resolve().each { artifact ->
            copy {
                from { artifact }
                into file("${buildDir}/rustLibs/native")
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-h", file("${buildDir}/headers")]
}

sourceSets.main.resources {
    srcDir(file("${buildDir}/rustLibs/native"))
}

processResources.dependsOn(collectLibs)

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        main(MavenPublication) {
            artifactId = project.name
            from(components["java"])
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                name = 'Unity Version Manager JNI'
                description = 'A JNI library to execute unity version manager from java.'
                url = 'https://github.com/wooga/unity-version-manager-jni'

                artifactId = project.name
                inceptionYear = "2018"

                scm {
                    connection = 'scm:git:https://github.com/wooga/unity-version-manager-jni.git'
                    developerConnection = 'scm:git:https://github.com/wooga/unity-version-manager-jni.git'
                    url = 'https://github.com/wooga/unity-version-manager-jni.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'manfred.endres'
                        name = 'Manfred Endres'
                        email = 'manfred.endres@wooga.net'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
        }
    }
}

signing {
    def signingKeyId = project.hasProperty("signingKeyId") ? project.property('signingKeyId') : System.getenv('OSSRH_SIGNING_KEY_ID')
    def signingKey = project.hasProperty("signingKey") ? project.property('signingKey') : System.getenv('OSSRH_SIGNING_KEY')
    def signingPassword = project.hasProperty('signingPassphrase') ? project.property('signingPassphrase') : System.getenv("OSSRH_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKeyId.toString(), signingKey.toString(), signingPassword.toString())
    sign publishing.publications.main
}

postRelease.dependsOn(tasks.publish)

afterEvaluate {
    tasks."final".dependsOn(tasks.publishToSonatype, tasks.closeAndReleaseSonatypeStagingRepository)
    tasks."candidate".dependsOn(tasks.publishToSonatype, tasks.closeAndReleaseSonatypeStagingRepository)
    tasks.publishToSonatype.mustRunAfter(tasks.postRelease)
    tasks.closeSonatypeStagingRepository.mustRunAfter(tasks.publishToSonatype)
    tasks.publish.mustRunAfter(tasks.release)
}
